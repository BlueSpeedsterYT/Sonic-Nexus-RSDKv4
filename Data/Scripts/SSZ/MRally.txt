// ----------------------------------
// RSDK Project:          Sonic Nexusv4
// Script Description:    Moto Rally Badnik
// Script Author:         Twanvanb1
// ----------------------------------

// ========================
// Aliases
// ========================

private alias Object.Value0 : Object.Timer

private alias 33 : TYPE_MRALLY

private alias 9 : ANI_SPINDASH
private alias 10 : ANI_JUMPING
private alias 15 : TYPE_EXPLOSION
private alias 20 : TYPE_INVINCIBLE

//----Player Movement States----//
private alias 8 : GETTINGHURT
private alias Object.Value4 : Player.Invincibility

// Player Alias
private alias object.xpos			 :	player.xpos

// ========================
// Function Declarations
// ========================

reserve function MRally_DebugDraw
reserve function MRally_DebugSpawn

private function MRally_DebugDraw
	DrawSprite(0)
end function


private function MRally_DebugSpawn
	CreateTempObject(TypeName[Moto Rally], 0, object.xpos, object.ypos)
end function

event ObjectUpdate

	switch Object.State
	case 0	//Moving Left
		Object.XPos -= 65536
		Object.Frame++

		if object[0].XPos < Object.XPos
			temp0 = Object.XPos
			temp0 -= object[0].XPos
			if temp0 < 4194304
				Object.State = 2
			end if
		end if

		ObjectTileGrip(0, -8, 16, 0)
		if CheckResult == 0
			Object.State = 4
		end if
		break
	case 1	//Moving Right
		Object.XPos += 65536
		Object.Frame++

		if object[0].XPos > Object.XPos
			temp0 = object[0].XPos
			temp0 -= Object.XPos
			if temp0 < 4194304
				Object.State = 3
			end if
		end if

		ObjectTileGrip(0, 8, 16, 0)
		if CheckResult == 0
			Object.State = 5
		end if
		break
	case 2	//Moving Left Fast
		Object.XPos -= 131072
		Object.Frame += 2
		ObjectTileGrip(0, -8, 16, 0)
		if CheckResult == 0
			Object.State = 4
		end if
		break
	case 3	//Moving Right Fast
		Object.XPos += 131072
		Object.Frame += 2
		ObjectTileGrip(0, 8, 16, 0)
		if CheckResult == 0
			Object.State = 5
		end if
		break
	case 4	//Waiting Left
		Object.Timer++
		if Object.Timer == 30
			Object.Timer = 0
			Object.State = 1
			Object.Direction = 2
		end if
		break
	case 5	//Waiting Right
		Object.Timer++
		if Object.Timer == 30
			Object.Timer = 0
			Object.State = 0
			Object.Direction = 0
		end if
		break
	end switch

    foreach (GROUP_PLAYERS, currentPlayer, ACTIVE_ENTITIES)
		BoxCollisionTest(C_TOUCH, object.entityPos, -22, -8, 22, 8, currentPlayer, C_BOX, C_BOX, C_BOX, C_BOX)
	
		if checkResult == true
			CallFunction(Player_BadnikBreak)
		end if
	next

end event 

event ObjectDraw

	if Object.Frame > 15
		Object.Frame = 0
	end if

	Temp0 = Object.Frame
	Temp0 >>= 3
	Temp0 += Object.Direction
	DrawSprite(Temp0)

end event

event ObjectStartup
	LoadSpriteSheet("SSZ/Objects.gif")
	
	//Moto Rally Frames
	SpriteFrame(-24, -16, 47, 32, 100, 1)
	SpriteFrame(-24, -16, 47, 32, 100, 34)

	SpriteFrame(-24, -16, 47, 32, 100, 67)
	SpriteFrame(-24, -16, 47, 32, 100, 100)

	foreach (TypeName[Moto Rally], arrayPos0, ALL_ENTITIES)
	    Object[ArrayPos0].xvel = -0x10000
	next

	SetTableValue(TypeName[Moto Rally], DebugMode_ObjCount, DebugMode_TypesTable)
	SetTableValue(MRally_DebugDraw, DebugMode_ObjCount, DebugMode_DrawTable)
	SetTableValue(MRally_DebugSpawn, DebugMode_ObjCount, DebugMode_SpawnTable)
	DebugMode_ObjCount++
end event

// ========================
// Editor Events
// ========================

event RSDKDraw
	DrawSprite(0)
end event


event RSDKLoad
	LoadSpriteSheet("SSZ/Objects.gif")
	SpriteFrame(-24, -16, 47, 32, 100, 1)
	
	SetVariableAlias(ALIAS_VAR_PROPVAL, "unused")
end event